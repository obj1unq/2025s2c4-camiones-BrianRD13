import camion.*
import cosas.*

describe "Carga y Descarga" {
  test "Cargar Bumblebee y verificar si esta solo" {
    camion.cargar(bumblebee)
    assert.equals(#{bumblebee}, camion.cosas())
    }

  test "Cargar Knight Rider y verificar si estan ambos" {
    camion.cargar(bumblebee)
    // Ya esta cargado Bumblebee

    camion.cargar(knightRider)
    assert.equals(#{bumblebee, knightRider}, camion.cosas())
  }

  test "Descargar Bumblebee y verificar si esta solo Knight Rider" {
    camion.cargar(bumblebee)
    camion.cargar(knightRider)
    // Ya estan cargados Bumblebee y Knight Rider

    camion.descargar(bumblebee)
    assert.equals(#{knightRider}, camion.cosas())
  }

  test "Cargar Knight Rider y descargar Bumblebee, verificando que ambas operaciones fallen" {
    camion.cargar(knightRider)
    // Ya esta cargado knightRider
    
    assert.throwsException({ camion.cargar(knightRider) })
    assert.throwsException({ camion.descargar(bumblebee) })
  }
}

describe "Todo peso par" {
  test "Un camion cargado con Bumblebee y 50 kg de arena a granel deberia ser par" {
    arenaAGranel.cambiarPeso(50)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)

    assert.that(camion.todoPesoPar())
  }

  test "En cambio, si la arena a granel pesa 51 kg entonces no" {
    arenaAGranel.cambiarPeso(51)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)

    assert.notThat(camion.todoPesoPar())    
  }
}

describe "Hay alguno que pesa" {
  method initialize() {
    arenaAGranel.cambiarPeso(50)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)
  }
  test "Se carga a Bumblebee y 50 kg de arena a granel, luego se pregunta por 50 kg y debe dar verdadero" {
    camion.initialize()
    assert.that(camion.objetoConPeso(50))
  }

  test "Se pregunta por 800 kg y debe dar verdadero, debido a Bumblebee" {
    camion.initialize()
    assert.that(camion.objetoConPeso(800))
  }

  test "Se pregunta por 500 kg y debe dar falso" {
    camion.initialize()
    assert.notThat(camion.objetoConPeso(500))
  }
}

describe "Peso y exceso de peso" {
  test "Si el camion tiene a Bumblebee y un paquete de 300 ladrillos, entonces el peso total es 2400 y no está excedido de peso" {
    camion.cargar(bumblebee)
    paqueteDeLadrillos.agregarLadrillos(300)
    camion.cargar(paqueteDeLadrillos)

    assert.equals(2400, camion.peso())
    assert.notThat(camion.excedidoDePeso())
  }

  test "Pero si el paquete de ladrillos contiene 400 ladrillos, entonces el peso total es 2600 y sí está excedido" {
    camion.cargar(bumblebee)
    paqueteDeLadrillos.agregarLadrillos(400)
    camion.cargar(paqueteDeLadrillos)

    assert.equals(2600, camion.peso())
    assert.that(camion.excedidoDePeso())
  }
}

describe "El de nivel" {
  method initialize() {
    camion.cargar(bumblebee) // modo auto
    camion.cargar(bateriaAntiaerea) // descargada
    camion.cargar(residuosRadiactivos)
  }

  test "Para un camión cargado con Bumblebee en modo auto, la batería antiaérea descargada y los residuos radiactivos, si se pide algo de nivel 0 debe encontrar a la batería antiaérea" {
    camion.initialize()
    assert.equals(bateriaAntiaerea, camion.objetoConPeligrosidad(0))
  }

  test "Si se pide algo de nivel 15 debe encontrar a Bumblebee" {
    camion.initialize()
    assert.equals(bumblebee, camion.objetoConPeligrosidad(15))
    
  }

  test "Si se pide algo de nivel 200 debe encontrar a los residuos" {
    camion.initialize()
    assert.equals(residuosRadiactivos, camion.objetoConPeligrosidad(200))
  }

  test "Si se pide algo de nivel 30 debe romper, porque no se encuentra nada" {
    camion.initialize()
    assert.throwsException({ camion.objetoConPeligrosidad(30) })
  }

  test "Si se cambia a Bumblebee a modo robot y se vuelve a pedir algo de 30, entonces encuentra a Bumblebee" {
    camion.initialize()
    bumblebee.transformarse() // modo robot
    assert.equals(bumblebee, camion.objetoConPeligrosidad(30))
  }

  test "Si se carga la batería con misiles y se pide algo de peligrosidad 100 entonces encuentra a la batería" {
    camion.initialize()
    bateriaAntiaerea.cambiarEstado() // con misiles
    assert.equals(bateriaAntiaerea, camion.objetoConPeligrosidad(100))
  }
}

describe "Cosas peligrosas" {
  method initialize() {
    camion.cargar(knightRider) // 20
    bateriaAntiaerea.cambiarEstado() // con misiles, 100
    camion.cargar(bateriaAntiaerea)
  }
  test "Para un camión cargado con Knight Rider y la batería antiaérea cargada, en las cosas más peligrosas de 15 sólo está la batería antiaérea" {
    camion.initialize()

    assert.equals(#{bateriaAntiaerea}, camion.objetosMasPeligrososQue(15))
  }

  test "En las cosas más peligrosas que Bumblebee sólo está la batería antiaérea." {
    camion.initialize()

    assert.equals(#{bateriaAntiaerea}, camion.objetosMasPeligrososQue(bumblebee))  
  }

  test "Las cosas que superan 2 de peligrosidad son Knight Rider y la batería antiaérea." {
    camion.initialize()

    assert.equals(#{knightRider, bateriaAntiaerea}, camion.objetosMasPeligrososQue(2))
  }

  test "Las cosas más peligrosas que el paquete de ladrillos son Knight Rider y la batería antiaérea." {
    camion.initialize()
    paqueteDeLadrillos.agregarLadrillos(100) // peligrosidad 2

    assert.equals(#{knightRider, bateriaAntiaerea}, camion.objetosMasPeligrososQue(paqueteDeLadrillos))    
  }

  test "No hay cosas más peligrosas que 200." {
    camion.initialize()

    assert.equals(#{}, camion.objetosMasPeligrososQue(200))    
  }

  test "No hay nada más peligroso que los residuos radiactivos." {
    camion.initialize()

    assert.equals(#{}, camion.objetosMasPeligrososQue(residuosRadiactivos))  
  }
}

describe "Puede circular en ruta" {
  test "Si el camión tiene a Bumblebee en auto y un paquete de 300 ladrillos, puede circular en una ruta con nivel 20." {
    camion.cargar(bumblebee) // modo auto
    paqueteDeLadrillos.agregarLadrillos(300) // peligrosidad 2, peso 600
    camion.cargar(paqueteDeLadrillos)

    assert.that(camion.circularEnRuta())    
  }

  test "Pero si el camión tiene el paquete de 400 ladrillos y Bumblebee en modo auto ya no puede circular en la ruta de nivel 20, por estar excedido de peso." {
    camion.cargar(bumblebee) // modo auto
    paqueteDeLadrillos.agregarLadrillos(400) // peligrosidad 2, peso 800
    camion.cargar(paqueteDeLadrillos)

    assert.notThat(camion.circularEnRuta())    
  }

  test "Tampoco puede circular en una ruta de nivel 20 si tiene un paquete de 300 ladrillos y Bumblebee está en modo robot." {
    bumblebee.transformarse() // modo robot
    camion.cargar(bumblebee)
    paqueteDeLadrillos.agregarLadrillos(300) // peligrosidad 2, peso 600
    camion.cargar(paqueteDeLadrillos)

    assert.notThat(camion.circularEnRuta())
  }
}